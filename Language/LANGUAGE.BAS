10 CLS : PRINT "LANGUAGE": INPUT "INPUT THRESHOLD (DEFAULT=0)"; TR
11 B1 = 1.184015211#: B2 = .800417167#: B3 = 18.43103375#: B4 = .112255309#: B5 = 1.625639319#: B6 = 1.476639897#: HF = .5
12 INPUT "DO YOU WANT RECOGNITION ROUTINE (Y/N)"; RR$: IF RR$ = "y" THEN RR$ = "Y"
20 Z = 0: W = 1: XT = W: TW = 2: TH = 3: T = 100: TT = 1000: EI = 8: ML = TW: HI = W: HJ = W: TC = TT: TY = 36: C1 = B1 + B2 + B5: C2 = TW * (B1 + B2): T3 = TT

30 REM S$() HOLDS THE I/P STRING, L$() HOLDS THE LEXICON. PL%() HOLDS LEXICON FREQUENCIES.

40 REM THIS SECTION TAKES IN THE I/P STRING AND BUILDS UP A LEXICON AND FREQUENCY COUNT
50 DIM S$(2000), L$(T), PL%(T), L%(T, T), X%(TT), I(T), B%(30, TY), KF%(TY): DEF FNKM (A) = SGN(A) - W: DEF FNKP (A) = W - SGN(A - W)

60 INPUT "HOW MANY CHARACTERS ARE YOU ENTERING AT A TIME"; SC
61 INPUT "DO YOU HOLD THE CHARACTERS ON TAPE (Y/N)"; A$
62 IF A$ = "Y" OR A$ = "y" THEN 70
63 GOTO 80

70 PRINT "RECALL X%": REM LOAD ROUTINE
71 M$ =  _
"ITISSUMMERTIMESCHOOLISOVERANDTHELONGSUMMERHOLIDAYISHEREJANEANDPETERTALKABOUTTHEIRLONGSUMMERHOLIDAYANDWHATTHEYAREGOINGTODOILIKESCHOOLSAYSPETERBUTIAMGLADTHEHOLIDAYHASCOMEYESIAMGLADTOOSAYSJANEILIKESUNNYDAYSWHENWEHAVENOWORKTODOTHEREARESOMANYNICETHINGSTODOINTHEHOLIDAYWHENITISSUNNYYESSAYSPETERANDDADTHINKSITDOESUSGOODTOGETOUTINTHESUNWEWILLBEOUTEVERYDAYWHENTHESUNCOMESOUTDOYOUKNOWTHEREISANOLDDONKEYUPATTHEF" _


72 SC = LEN(M$): PRINT SC; " CHACTERS ENTERED": FOR I = 1 TO SC: X%(I) = ASC(MID$(M$, I, 1)): NEXT I
73 FOR I = W TO SC: S$(I) = CHR$(X%(I)): PRINT S$(I); : NEXT I: FOR I = SC + W TO TT: S$(I) = CHR$(X%(I)): NEXT I: GOTO 95

80 FOR I = W TO SC
81 M$ = INKEY$: IF M$ = "" THEN 81
83 S$(I) = M$
85 X%(I) = ASC(S$(I)): IF ASC(S$(I)) = EI THEN IF I > W THEN I = I - 2: S$(I) = CHR$(8)
90 PRINT S$(I); : NEXT I

91 PRINT : INPUT "DO YOU WISH TO STORE THESE CHARACTERS ON TAPE (Y/N)"; A$
92 IF A$ = "Y" OR A$ = "y" THEN PRINT "STORE X%": STOP

94 REM THE NEXT SECTION SETS UP L%(I,J) AS A JOINT PROBABILITY MATRIX.
95 HI = Z: FOR I = W TO SC
110 FOR J = Z TO LC
120 IF S$(I) <> L$(J) THEN TM = LC + 1: GOTO 122
121 TM = J: J = LC
122 NEXT J: J = TM
130 PL%(J) = PL%(J) + W: IF J = LC + W THEN LC = LC + W: L$(LC) = S$(I): REM I OR J?
140 IF HI THEN L%(HI, J) = L%(HI, J) + W: L%(HI, Z) = L%(HI, Z) + FNKP(L%(HI, J)): L%(Z, J) = L%(Z, J) + FNKP(L%(HI, J))
150 HI = J: NEXT I: PRINT
160 HI = W: HJ = W: GOSUB 700: GOSUB 800

326 NX = SC - W: SL = LC * (LC + W) / TW: NC = Z: FOR I = W TO LC: I(I) = I: NC = NC + L%(I, Z): NEXT I
329 FOR I = TW TO LC: C = W: CC = I(I - W)
330 IF PL%(I) = PL%(I - W) THEN CC = CC + I(I): C = C + W: I = I + W: IF I <= LC THEN 330
331 IF C > W THEN FOR J = I - C TO I - W: I(J) = CC / C: NEXT J
332 NEXT I: D3 = LC + HF: D1 = B3 / SL: D2 = B4 / SL: HH = TR - C1 * LOG(NC) + C2 * LOG(LC) + B5 * LOG(NX) + B6: H = HH
333 FOR I = W TO LC: FA = B1 * LOG(D3 - L%(I, Z)) + D1 * I(I): FOR J = W TO LC
334 IF L%(I, J) THEN FF = FA + B2 * LOG(D3 - L%(Z, J)) + D2 * I(J) + B5 * LOG(L%(I, J)): IF FF > HH THEN HI = I: HJ = J: HH = FF
340 NEXT J: NEXT I: IF HH > H THEN 347
345 IF RR$ = "Y" THEN GOSUB 2000: HI = W: HJ = W: GOSUB 700: GOTO 326
347 HA = L%(HI, HJ): HB = PL%(HI) - HA: HC = PL%(HJ) - HA: H = HH - H + TR: PRINT "COEFFICIENT FF ="; H
349 PRINT "LEXICON ADDITION IS: "; L$(HI) + L$(HJ)
370 LC = LC + W: L$(LC) = L$(HI) + L$(HJ)
465 IF LEN(L$(LC)) > ML THEN ML = LEN(L$(LC))
490 I = W
500 IW = I + W: IF S$(I) + S$(IW) = L$(LC) THEN S$(I) = L$(LC): FOR J = IW TO SC - W: S$(J) = S$(J + W): NEXT J: GOSUB 900: SC = SC - W
510 PRINT S$(I); " ";
520 I = IW: IF I < SC THEN 500
530 IF I = SC THEN PRINT S$(I)
534 TC = TC - HA: FOR I = SC + W TO TC: S$(I) = S$(I + HA): NEXT I
540 PL%(LC) = PL%(LC) + HA: L%(HI, HJ) = Z: PL%(HI) = PL%(HI) - PL%(LC): PL%(HJ) = PL%(HJ) - PL%(LC): L%(HI, Z) = L%(HI, Z) - W: L%(Z, HJ) = L%(Z, HJ) - W
550 PRINT : PRINT "SC ="; SC
551 IF INKEY$ <> "" THEN PRINT : INPUT "HIT ENTER TO CONTINUE (ENTER Q TO QUIT)"; M$: IF M$ = "Q" THEN END
555 GOSUB 700: GOSUB 800
570 GOTO 326

700 REM SORT ROUTINE
710 IF HJ < HI THEN HI = HJ
715 FOR I = W TO LC - HI: C = Z
720 FOR J = HI TO LC - I: JW = J + W
730 IF PL%(J) < PL%(JW) THEN 740
731 GOTO 750
740 H = PL%(J): PL%(J) = PL%(JW): PL%(JW) = H: A$ = L$(J): L$(J) = L$(JW): L$(JW) = A$
741 FOR K = Z TO LC: H = L%(K, J): L%(K, J) = L%(K, JW): L%(K, JW) = H: NEXT K
742 FOR K = Z TO LC: H = L%(J, K): L%(J, K) = L%(JW, K): L%(JW, K) = H: NEXT K: C = C + W
750 NEXT J: IF C THEN TM = LC - HI + 1: GOTO 755
751 TM = I: I = LC - HI
755 NEXT I: I = TM
760 IF PL%(LC) = Z THEN L$(LC) = "": LC = LC - W: GOTO 760
770 RETURN

800 REM PRINT LEXICON ROUTINE.
810 PRINT "LEXICON ="; LC
820 PRINT "I", "L$(I)"; "  FREQUENCY-PL%(I)"
830 FOR I = W TO LC
840 PRINT I, L$(I), PL%(I)
850 NEXT I: RETURN
900 REM READJUST L%(I,J) ROUTINE.
910 C = TW: FOR K = W TO LC
920 IF S$(I - W) = L$(K) THEN L%(K, LC) = L%(K, LC) + W: L%(K, HI) = L%(K, HI) - W: C = C - W: A = FNKP(L%(K, LC)): B = FNKM(L%(K, HI)): L%(K, Z) = L%(K, Z) + A: L%(Z, LC) = L%(Z, LC) + A: L%(K, Z) = L%(K, Z) + B: L%(Z, HI) = L%(Z, HI) + B
925 IF IW = SC THEN TM = LC + 1: GOTO 945
930 IF S$(IW) = L$(K) THEN L%(LC, K) = L%(LC, K) + W: L%(HJ, K) = L%(HJ, K) - W: C = C - W: A = FNKP(L%(LC, K)): B = FNKM(L%(HJ, K)): L%(LC, Z) = L%(LC, Z) + A: L%(Z, K) = L%(Z, K) + A: L%(HJ, Z) = L%(HJ, Z) + B: L%(Z, K) = L%(Z, K) + B
940 IF C THEN TM = LC + 1: GOTO 945
941 TM = K: K = LC
945 NEXT: K = TM
950 RETURN

2000 A$ = "": PRINT "STRING I/P TO RECOGNISER: "
2030 FOR I = SC TO SC - ML + W STEP -W
2040 A$ = S$(I) + A$: IF LEN(A$) < ML - W THEN TM = SC - ML + W - W: GOTO 2044
2041 TM = I: I = SC - ML + W
2044 NEXT I: I = TM
2045 MS = I: NN = SC - MS + ML
2050 J = Z: PRINT
2060 FOR I = MS - W TO SC + ML - W
2062 FOR K = W TO LC
2065 IF S$(I) <> L$(K) THEN TM = LC + 1: GOTO 2067
2066 TM = K: K = LC
2067 NEXT K: K = TM
2068 B%(J, Z) = K: IF K = LC + W THEN LC = LC + W: L$(LC) = S$(I)
2070 IF J > Z THEN PRINT L$(K); " "; : IF I <= SC THEN PL%(K) = PL%(K) - W: L%(K1, K) = L%(K1, K) - W: L%(K1, Z) = L%(K1, Z) + FNKM(L%(K1, K)): L%(Z, K) = L%(Z, K) + FNKM(L%(K1, K))
2080 IF I > SC THEN A$ = A$ + L$(K)
2090 K1 = K: J = J + W: NEXT I
2200 PRINT : MI = J - W
2210 X$ = A$: J = W: K = W: L0 = LEN(A$): TK = W: FOR I = W TO TY: KF%(I) = L0: NEXT I

2220 CO = Z: FOR I = W TO LC
2235 IF L$(I) <> LEFT$(X$, LEN(L$(I))) THEN TM = LC + 1: GOTO 2250
2238 TM = I + 1
2240 CO = CO + W: IF CO = W THEN B%(Z, K) = J: B%(J, K) = I: KF%(K) = KF%(K) - LEN(L$(I))
2242 IF CO <= W THEN TM = LC + 1: GOTO 2250
2243 TM = I + 1
2245 TK = TK + W: FOR Q = W TO J - W: B%(Q, TK) = B%(Q, K): NEXT Q: B%(J, TK) = I: KF%(TK) = KF%(K) - LEN(L$(I)) + LEN(L$(B%(J, K))): B%(Z, TK) = J
2249 IF TK > MK THEN MK = TK
2250 NEXT I: I = TM

2270 IF TK > W THEN GOSUB 2500: IF FL = 1 THEN 2380
2350 L1 = L0: FOR K = W TO TK: IF KF%(K) > Z AND KF%(K) <= L1 THEN L1 = KF%(K): KH = K
2370 NEXT K: IF L1 AND KF%(KH) THEN K = KH: X$ = RIGHT$(A$, L1): J = B%(Z, K) + W: GOTO 2220
2380 TI = TK: IF TK = W THEN OP = W: GOTO 2600
2400 HH = TT + TW: FOR I = W TO TK: E = Z: CH = Z: FOR J = W TO MI: E = E + PL%(B%(J, I)): NEXT J: E = E / MI: FOR J = W TO MI: CH = CH + ((PL%(B%(J, I)) - E) + TW) / E: NEXT J: IF CH < HH THEN OP = I: HH = CH
2401 REM -TW? *TW? +TW IN ABOVE?
2440 NEXT I: GOTO 2600

2500 REM RECOGNISER SHRINK
2502 FOR I = W TO TK: IF KF%(I) = Z AND B%(Z, I) < MI THEN MI = B%(Z, I)
2504 NEXT I
2510 FOR I = W TO TK
2520 IF B%(Z, I) > MI OR B%(Z, I) = MI AND KF%(I) THEN FOR Q = Z TO B%(Z, TK): B%(Q, I) = B%(Q, TK): NEXT Q: KF%(I) = KF%(TK): TK = TK - W: I = I - W
2523 IF I = TK THEN TM = I: I = TK: GOTO 2525
2524 TM = I + 1
2525 NEXT I: I = TM
2530 FOR I = W TO TK: IF KF%(I) THEN TM = I: I = TK: FL = 0: GOTO 2539
2538 FL = 1
2539 NEXT I: I = TM
2540 RETURN

2600 REM PRINT CHOSEN OPTION(S) AND THEN INTO 3000
2610 PRINT "RECOGNISER GIVES: ": PRINT : FOR I = W TO B%(Z, OP): PRINT L$(B%(I, OP)); " "; : NEXT I: PRINT
2620 IF TI > W THEN PRINT "TIE-BREAKER": STOP
3000 REM ROUTINE TO TAKE ON MORE INPUT
3020 SC = SC - NN + ML - W + B%(Z, OP)
3060 FOR I = W TO B%(Z, OP)
3070 PL%(B%(I, OP)) = PL%(B%(I, OP)) + W
3080 NEXT I
3090 A = B%(Z, Z): B = B%(W, OP)
3120 L%(A, B) = L%(A, B) + W: L%(A, Z) = L%(A, Z) + FNKP(L%(A, B)): L%(Z, B) = L%(Z, B) + FNKP(L%(A, B))
3125 IF B%(Z, OP) <= W THEN 3160
3130 FOR I = W TO B%(Z, OP) - W
3132 A = B%(I, OP): B = B%(I + W, OP)
3140 L%(A, B) = L%(A, B) + W: L%(A, Z) = L%(A, Z) + FNKP(L%(A, B)): L%(Z, B) = L%(Z, B) + FNKP(L%(A, B))
3150 NEXT I
3160 DI = NN - B%(Z, OP): TC = TC - DI: IF DI < Z THEN FOR I = TC TO SC + W STEP -W: S$(I) = S$(I + DI): NEXT I
3165 J = W
3170 FOR I = SC - B%(Z, OP) + W TO SC
3180 S$(I) = L$(B%(J, OP)): J = J + W
3190 NEXT I
3200 IF DI > Z THEN FOR I = SC + W TO TC: S$(I) = S$(I + DI): NEXT I
3220 RETURN

