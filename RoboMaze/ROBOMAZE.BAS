0 CLS:CLEAR 1000:KEY OFF:GOTO 20
3 LOCATE C1,C2:RETURN
4 REM ************************
5 REM * LISTING 5.1          *
6 REM * ROBOT MAZE LEARNER   * 
7 REM ************************
20 SW=40:SH=23:REM WIDTH & HEIGHT
30 DIM M$(SH+1,SW+1),V(SH+1,SW+1),R((SW+1)*(SH+1),1)
40 CLS:R$=CHR$(1):W$=CHR$(219):W=ASC(W$):BL$=" "
50 C1=1:FOR I=3 TO SW:C2=I:GOSUB 3:PRINT W$;:NEXT
60 FOR J=2 TO SH:C1=J:C2=SW:GOSUB 3:PRINT W$;:NEXT 
70 FOR I=SW-2 TO 1 STEP-1:C1=SH:C2=I:GOSUB 3:PRINT W$;:NEXT 
80 C2=1:FOR J=SH-1 TO 1 STEP-1:C1=J:GOSUB 3:PRINT W$;:NEXT
90 RANDOMIZE TIMER:FOR I=2 TO SW-1:REM RANDOMIZE TIMER
100 FOR J=2 TO SH-1
110 IF RND(1)<.25 THEN C1=J:C2=I:GOSUB 3:PRINT W$;
120 NEXT:NEXT
130 FOR I=2 TO 4:FOR J=2 TO 4:C1=I:C2=J:GOSUB 3:PRINT BL$;:C1=SH-I+1:C2=SW-J+1:GOSUB 3:PRINT BL$;:NEXT:NEXT
140 REM SEARCH MAZE USING SESNORS
150 LOCATE 24,1:PRINT "EXPLORING...                           ";
155 I=1:J=2
160 IF I=SH OR J=SW THEN 280
170 GOSUB 390:ME=ME+1:R(ME,0)=I:R(ME,1)=J:V(I,J)=V(I,J)+1:IF M$(I,J)="" THEN GOSUB 480
175 REM STORE ROUTE; HOW OFTEN VISIT EACH CELL?; USE SENSORS IF NOT THERE BEFORE
180 B$=M$(I,J)
190 IF V(I,J)=1 THEN 230
200 FOR K=ME-1 TO 1 STEP-1:IF R(K,0)=I THEN IF R(K,1)=J THEN ME=K:K=1
205 REM IF YOU'VE BEEN HERE BEFORE FORGET THE ROUTE THAT BROUGHT YOU HERE.
210 NEXT
220 ON V(I,J)-INT((V(I,J)-1)/4)*4 GOTO 230,240,250,260
230 IF MID$(B$,1,1)="0" THEN GOSUB 430:I=I+1:GOTO 160
240 IF MID$(B$,2,1)="0" THEN GOSUB 430:J=J+1:GOTO 160
250 IF MID$(B$,3,1)="0" THEN GOSUB 430:I=I-1:GOTO 160
260 IF MID$(B$,4,1)="0" THEN GOSUB 430:J=J-1
270 GOTO 160
280 GOSUB 390:ME=ME+1:R(ME,0)=I:R(ME,1)=J
290 LOCATE 24,1:PRINT "GOT THERE!                             ";
300 REM RE-RUN MAZE
310 GOSUB 430
320 FOR K=1 TO ME
330 I=R(K,0):J=R(K,1) 
340 GOSUB 390
350 FOR Q=1 TO 250:X=X+1-1:NEXT:REM DELAY
360 GOSUB 430
370 NEXT:GOSUB 390
380 LOCATE 24,1:PRINT "HOW'S THAT!                            ";:LOCATE 1,1:GOTO 600
390 REM POSITION ROBOT I,J
410 C1=I:C2=J:GOSUB 3:PRINT R$;:FOR Q=1 TO 25:NEXT:REM DELAY
420 RETURN
430 REM DELETE ROBOT
450 C1=I:C2=J:GOSUB 3:PRINT BL$;
460 RETURN
465 REM USE SENSORS TO BUILD MAP
480 V=I:H=J
500 IF SCREEN(V,H-1)=W THEN M$(I,J)="1":GOTO 510
501 M$(I,J)="0"
510 IF ME=1 THEN M$(I,J)="1"+M$(I,J):GOTO 540
520 IF SCREEN(V-1,H)=W THEN M$(I,J)="1"+M$(I,J):GOTO 540
521 M$(I,J)="0"+M$(I,J)
540 IF SCREEN(V,H+1)=W THEN M$(I,J)="1"+M$(I,J):GOTO 560
541 M$(I,J)="0"+M$(I,J)
560 IF SCREEN(V+1,H)=W THEN M$(I,J)="1"+M$(I,J):RETURN
561 M$(I,J)="0"+M$(I,J)
580 RETURN
600 LOCATE 24,1:PRINT,"AGAIN? (Y/N)                           ";
605 M$=INKEY$:IF M$="Y" OR M$="y" THEN RUN
610 IF M$="N" OR M$="n" THEN 620
615 GOTO 605
620 KEY ON:END
630 REM R. FORSYTH & C. NAYLOR
640 REM MC-10 EDITS JIM GERRIE
